{neoFlake}: let
  inherit (import ./extract_lib.nix {inherit neoFlake;}) neoConfig safeList sortByRank;
  inventory = import ./extract_plugin_inventory.nix {inherit neoFlake;};

  configServices = neoConfig.services or {};
  names = builtins.attrNames configServices;

  raw =
    map (n: let
      svc = configServices.${n};
      meta = svc.meta or {};
      enabled = svc.enabled or false;
    in {
      name = n;
      inherit enabled;
      # Runtime units for the grid status dot (installed services only).
      units =
        if enabled
        then safeList (svc.systemdUnits or [])
        else [];
      timers =
        if enabled
        then safeList (svc.systemdTimers or [])
        else [];
      icon = meta.icon or null;
      rank = meta.rank or null;
      category =
        if (meta.category or "") != ""
        then meta.category
        else "Other";
      description = meta.description or "";
      pluginUrls = inventory.owners.${n} or [];
    })
    names;

  # Preferred category order for the services grid UI.
  categoryOrder = [
    "Core"
    "Network"
    "Security"
    "Media"
    "Files"
    "Monitoring"
    "Utilities"
    "AI"
    "Other"
  ];

  # Within a category: installed first, then available; each group by rank/name.
  sortServices = services: let
    enabled = builtins.filter (s: s.enabled) services;
    disabled = builtins.filter (s: !s.enabled) services;
  in
    sortByRank enabled ++ sortByRank disabled;

  # Unique categories present, ordered by categoryOrder then alpha.
  orderedCategories = services: let
    present = builtins.attrNames (
      builtins.listToAttrs (
        map (s: {
          name = s.category;
          value = true;
        })
        services
      )
    );
    known = builtins.filter (c: builtins.elem c present) categoryOrder;
    unknown = builtins.sort (a: b: a < b) (
      builtins.filter (c: !(builtins.elem c categoryOrder)) present
    );
  in
    known ++ unknown;

  groupByCategory = services:
    map (cat: let
      svcs = sortServices (builtins.filter (s: s.category == cat) services);
    in {
      name = cat;
      services = svcs;
      hasEnabled = builtins.any (s: s.enabled) svcs;
      hasDisabled = builtins.any (s: !s.enabled) svcs;
    }) (orderedCategories services);
in {
  groups = groupByCategory raw;
  categories = orderedCategories raw;
  pluginInventory = inventory.plugins;
}
