{inputs, ...}: {
  flake.modules.nixos.disko = {
    config,
    lib,
    pkgs,
    ...
  }: let
    cfg = config.neo.disko;
    sanitize = n: lib.replaceStrings ["/" "-" "." " "] ["" "" "" ""] (baseNameOf n);
    volumesRoot = config.neo.core.volumes.root;
    # Restore of the whole Neo data dataset runs in the systemd initrd
    # (see zfs-restore.sh). The system itself (root, /nix) is never snapshotted.
    dataRestore = cfg.enabled && config.boot.initrd.systemd.enable;
    dataDataset = "${cfg.poolName}/neo";
  in {
    imports = [inputs.disko.nixosModules.disko];
    disko.devices = lib.mkIf cfg.enabled {
      disk =
        {
          main = {
            type = "disk";
            device = cfg.mainDisk;
            content = {
              type = "gpt";
              partitions = {
                ESP = {
                  size = "1G";
                  type = "EF00";
                  content = {
                    type = "filesystem";
                    format = "vfat";
                    mountpoint = "/boot";
                    mountOptions = ["umask=0077"];
                  };
                };
                zfs = {
                  size = "100%";
                  content = {
                    type = "zfs";
                    pool = cfg.poolName;
                  };
                };
              };
            };
          };
        }
        // lib.mapAttrs' (disk: mp: {
          name = "disk-${sanitize disk}";
          value = {
            type = "disk";
            device = disk;
            content = {
              type = "gpt";
              partitions = {
                zfs = {
                  size = "100%";
                  content = {
                    type = "zfs";
                    pool = "zpool-${sanitize mp}";
                  };
                };
              };
            };
          };
        })
        cfg.additionalDisks;

      zpool =
        {
          ${cfg.poolName} = {
            type = "zpool";
            mountpoint = null;
            rootFsOptions = {
              compression = "zstd";
              "com.sun:auto-snapshot" = "false";
            };
            datasets = {
              root = {
                type = "zfs_fs";
                mountpoint = "/";
                options."com.sun:auto-snapshot" = "false";
              };
              neo = {
                type = "zfs_fs";
                mountpoint = volumesRoot;
                options."com.sun:auto-snapshot" = "true";
              };
            };
          };
        }
        // lib.mapAttrs' (disk: mp: let
          pool = "zpool-${sanitize mp}";
        in {
          name = pool;
          value = {
            type = "zpool";
            mountpoint = null;
            rootFsOptions = {
              compression = "zstd";
              "com.sun:auto-snapshot" = "false";
            };
            datasets = {
              data = {
                type = "zfs_fs";
                mountpoint = mp;
                options."com.sun:auto-snapshot" = "false";
              };
            };
          };
        })
        cfg.additionalDisks;
    };

    networking.hostId = "4d681778";

    boot.supportedFilesystems = lib.mkIf cfg.enabled ["zfs"];
    boot.zfs = lib.mkIf cfg.enabled {
      forceImportRoot = lib.mkDefault false;
      forceImportAll = lib.mkDefault false;
    };

    environment.systemPackages = lib.mkIf cfg.enabled [pkgs.zfs];

    boot.initrd.systemd.services = lib.mkIf dataRestore {
      neo-zfs-restore = {
        description = "Neo: restore Neo data to a ZFS snapshot (scheduled from the web UI)";
        wantedBy = ["initrd.target"];
        requires = ["zfs-import-${cfg.poolName}.service"];
        after = ["zfs-import-${cfg.poolName}.service"];
        before = ["sysroot.mount" "initrd-root-fs.target" "initrd-fs.target"];
        unitConfig.DefaultDependencies = "no";
        serviceConfig.Type = "oneshot";
        environment.NEO_POOL = cfg.poolName;
        script = builtins.readFile ./zfs-restore.sh;
      };
    };

    # neo-web: the full data restore is offered only where the initrd hook exists.
    systemd.services.neo-web.environment = lib.mkIf (dataRestore && config.neo.services.neo.enabled) {
      NEO_ZFS_RESTORE_POOL = cfg.poolName;
      NEO_ZFS_RESTORE_DATASET = dataDataset;
    };

    services.zfs.autoSnapshot = lib.mkIf cfg.enabled {
      enable = true;
      flags = "-k -p --utc";
      frequent = 4;
      hourly = 12;
      daily = 7;
      weekly = 4;
      monthly = 3;
    };

    # Disko owns the root dataset; ensure the Neo data volume mountpoints only.
    system.activationScripts.create-volumes = lib.mkIf cfg.enabled (lib.mkForce (
      lib.neo.mkEnsureDirs config [
        config.neo.core.volumes.data
        config.neo.core.volumes.appdata
        config.neo.core.volumes.media
        config.neo.core.volumes.documents
      ]
    ));
  };
}
